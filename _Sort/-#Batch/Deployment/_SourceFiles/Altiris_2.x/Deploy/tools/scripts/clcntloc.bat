@echo off
:: ==============================================================
:: CLCNTLOC.BAT - This script counts the total number of internal 
::	      	  disks and local volumes on the primary node. 
::
:: Input:
::   None
::
:: Output:
::   skipfile.bat = a batch file that sets environment variables 
::    indicating the number of lines to skip in the disk and volume
::    output files duiring the diskp artitioning script cldepsp.bat.
::    This file is located in c:\OEM$\clusters on the target.
::
:: 1) All relevant temporary files are located in c:\$OEM$\clusters\temp
:: 2) The default number of internal disks is 1. (Windows 2000)
:: 3) The default number of volumes is 2. (Windows 2000)
:: ==============================================================
SET TOOLS=C:\$OEM$
SET DISKPATH=c:\$OEM$\clusters\temp
SET DPSCRIPT=%DISKPATH%\DPSCRIPT.txt
SET OUTFILE=%DISKPATH%\OUTFILE.txt
SET TEMPFILE=%DISKPATH%\TEMPFILE.txt
SET SKIPFILE=%DISKPATH%\SKIPFILE.bat
SET LOCALDSK=1
SET LOCALVOL=2

if "%WINDIR%"=="C:\WINDOWS" set DPART=c:\WINDOWS\SYSTEM32\DISKPART.EXE
if "%WINDIR%"=="C:\WINNT" set DPART=C:\$OEM$\DISKPART.EXE

:: ==============================================================
:: Check for the existence of the skipfile.bat. If present, then 
:: clcntloc.bat does not need to run again.
:: ==============================================================
if exist %DISKPATH%\skipfile.bat goto done
if NOT exist %DISKPATH% md %DISKPATH%

:: ==============================================================
:: Count the number of local drives and local volumes.
:: ==============================================================
echo Scanning for disks...
echo rescan> %DPSCRIPT%
%DPART% -s %DPSCRIPT%

echo Getting disk drive list...
echo list disk> %DPSCRIPT%
%DPART% -s %DPSCRIPT%>%OUTFILE%

echo Fixing disk list output file with MS-DOS new lines...
move %OUTFILE% %TEMPFILE%
%TOOLS%\SED.EXE -n -e "p" %TEMPFILE% > %OUTFILE%

echo Counting the number of local disks...
for /F "usebackq skip=7 tokens=2" %%i IN (`type %OUTFILE%`) do (
	SET LOCALDSK=%%i
	)
echo Getting volume list...
echo list volume> %DPSCRIPT%
%DPART% -s %DPSCRIPT%>%OUTFILE%

echo Fixing volume list output file with MS-DOS new lines...
move %OUTFILE% %TEMPFILE%
%TOOLS%\SED.EXE -n -e "p" %TEMPFILE% > %OUTFILE%
del %TEMPFILE%

echo Counting the number of local volumes...
for /F "usebackq skip=7 tokens=2" %%j IN (`type %OUTFILE%`) do (
	set LOCALVOL=%%j
	)

:: ==============================================================
:: The values of LOCALDSK, LOCALVOL represent the number of lines 
:: to skip in the output file generated after the shared storage 
:: has been configured. The output generated by DISKPART in 
:: Windows 2000 contains two more lines than the output generated
:: in Windows 2003.
:: ==============================================================
if "%WINDIR%"=="C:\WINDOWS" set NUMLINES=8
if "%WINDIR%"=="C:\WINNT" set NUMLINES=10
set /A LOCALDSK+=%NUMLINES%
set /A LOCALVOL+=%NUMLINES%

:: ==============================================================
:: Create the batch script to set skip variables
:: ==============================================================
echo SET SKIPDISK=%LOCALDSK% > %SKIPFILE%
echo SET SKIPVOL=%LOCALVOL% >> %SKIPFILE%

:done
SET SEVERITY=1
SET STATUS="Counted local disks and volumes on %COMPUTERNAME%."
IF EXIST c:\altiris\aclient\WLOGEVENT.EXE c:\altiris\aclient\WLOGEVENT.EXE -c:0 -l:%SEVERITY% -ss:"%STATUS%"
